BASE <https://lod.opentransportdata.swiss/nova/>
PREFIX rr: <http://www.w3.org/ns/r2rml#>
PREFIX rml: <http://semweb.mmlab.be/ns/rml#>
PREFIX ql: <http://semweb.mmlab.be/ns/ql#>
PREFIX carml: <http://carml.taxonic.com/carml/>
PREFIX nova: <http://lod.opentransportdata.swiss/schema/>
PREFIX sem: <http://semanticweb.cs.vu.nl/2009/11/sem/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX schema: <http://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ex: <http://example.org/>
PREFIX gtfs: <http://vocab.gtfs.org/terms#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

INSERT { GRAPH <http://example.org/inference> {
    ?hop a nova:Hop ;
        schema:validFrom ?validFrom ;
        schema:validThrough ?validThrough ;
        nova:subjectToSupplement ?zuschlagspflichtig ;
        nova:railwayReplacement ?bahnersatz ;
        nova:uic ?didok1 ;
        nova:uic ?didok2 ;
        nova:operator ?verwaltung .

}}
WHERE { GRAPH <http://example.org/nova> {
    ?hop a ex:KanteVersion ;
        ex:validFrom ?validFrom ;
        ex:validThrough ?validThrough ;
        ex:zuschlagspflichtig ?zuschlagspflichtig ;
        ex:bahnersatz ?bahnersatz ;
        ex:haltestelle1/ex:version/ex:sameAs ?didok1 ;
        ex:haltestelle2/ex:version/ex:sameAs ?didok2 ;
        ex:verwaltung/ex:version ?verwaltung .

        # TODO:
        # - verkehrsmittelTyp, eventuell aus GTFS Vocab

}}